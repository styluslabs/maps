apply plugin: 'com.android.application'

android {
  compileSdkVersion 30

  defaultConfig {
    applicationId 'com.styluslabs.maps'
    versionCode 1
    versionName "0.0.1"

    minSdkVersion 21
    targetSdkVersion 29
    externalNativeBuild {
      cmake {
        // Available arguments are inside ${SDK}/cmake/.../android.toolchain.cmake file
        targets 'droidmaps'
        arguments '-DTANGRAM_PLATFORM=android',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-fvisibility=hidden', // export only JNIEXPORT
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 // linker
                 '-Wl,--exclude-libs,ALL', // dont export static lib symbols
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wno-missing-field-initializers',
                 '-Wno-format-pedantic',
                 '-Wno-gnu-statement-expression',
                 '-Wno-gnu-anonymous-struct',
                 '-Wno-nested-anon-types',
                 '-Wno-unused-command-line-argument', // for -Wl linker flags..
                 '-Wno-unknown-warning-option'
        abiFilters 'arm64-v8a'
      }
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.debug
      debuggable true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
    }
  }

  externalNativeBuild {
    cmake {
      version '3.24.2'
      path '../../../CMakeLists.txt'
    }
  }

  sourceSets.main {
    assets.srcDirs = ['../../../assets']
  }

  androidResources {
    ignoreAssetsPattern '!old-*:!tilemaker'
  }

  lintOptions {
    abortOnError false
    disable 'ExpiredTargetSdkVersion'
  }
}

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.12.0'
  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'androidx.core:core:1.3.2'
}
