apply plugin: 'com.android.application'

android {
  compileSdkVersion 34
  namespace 'com.styluslabs.maps'
  defaultConfig {
    applicationId 'com.styluslabs.maps'
    // can't use env vars because of daemon
    versionCode project.getProperty("app_versioncode").toInteger()
    versionName String.valueOf(project.getProperty("app_versionname"))
    minSdkVersion 26
    targetSdkVersion 33
  }

  buildTypes {
    release {
      signingConfig signingConfigs.debug
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
    }
  }

  //ndkVersion '26.3.11579264'

  sourceSets.main {
    assets.srcDirs = ['../../../assets']
  }

  androidResources {
    ignoreAssetsPattern '!old-*:!.git'
  }

  buildFeatures {
    buildConfig true
  }

  lint {
    abortOnError false
    fatal 'NewApi'
    disable 'ExpiredTargetSdkVersion'
  }
}

task generateNativeLib(type: Exec) {
    commandLine 'make', 'ANDROID=1', 'OUTPUT=app/android/app/src/main/jniLibs/arm64-v8a/libdroidmaps.so', 'stripped'
    workingDir '../../..'
}

preBuild.dependsOn generateNativeLib

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.12.13'
  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'androidx.core:core:1.3.2'
}
